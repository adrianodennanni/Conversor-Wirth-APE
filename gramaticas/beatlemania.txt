Program = Block .
Block = "LET" "IT" "BE" { Declaration} StatementList "THE" "END" .
Declaration = ("WORDS" "OF" "WISDOM" | "NUMBER" "NINE") id { "," id } ";" .
StatementList = [id ":"] [Statement] {";" [id ":"] [Statement]} .
Statement =  Assignment | Transfer | Conditional | Write | Block .
Assignment = Expression "=>" id { "=>" id} .
Transfer = "GO" "HOME" id.
Conditional = "WHAT" "WOULD" "YOU" "DO" "IF" Expression "THEN" "YOU" "BEGIN" StatementList ["ELSE" StatementList] "FI" .
Write = "TWIST" "AND" "SHOUT" "(" Expression {"," Expression} ")" .
Expression = Exp1 [ ( "<" | ">" | "=" ) Exp1 ].
Exp1 = Exp2 { ("+" | "-" ) Exp2 } .
Exp2 = Exp3 { ("*" | "/" ) Exp3 } .
Exp3 = ["-"] ( "INPUT" | id | num | "(" Expression ")" )  .
id = ( "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" | "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z" ) { "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" | "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z" | "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" } .
num = ("0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ) {"0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" } .
